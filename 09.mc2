// @endpoint https://warp.cityzendata.net/api/v0/exec
@training/dataset1
'POLYGON ((-4.560135 48.301816, -4.560435 48.459353, -4.396476 48.474767, -4.257324 48.271512, -4.560135 48.301816))' 0.01 false GEO.WKT
GEO.REGEXP '~(' SWAP + ')' +
'regexp' STORE

$TOKEN AUTHENTICATE 1000000 MAXOPS

[ $TOKEN 'data.fuel' { 'type' 'gazole' 'loc' $regexp } NOW -1 ] FETCH 'data' STORE
$data
NOW ISO8601
'2019-01-01T00:00:00.000000Z'
TIMECLIP
NONEMPTY

[ SWAP 
  <%
    // Extract lat + lon
    [ 3 7 ] SUBLIST FLATTEN DUP
    [ 1 2 ] SUBLIST LIST-> DROP
    48.406879 -4.418599
    HAVERSINE 'dist' STORE
    <% $dist 10000.0 > %>
    <% NULL 4 SET %> // put null if too far
    IFT
  %> MACROMAPPER 0 0 0 
] MAP NONEMPTY 

[ SWAP 
  bucketizer.min
  0
  31536000000000
  0
] BUCKETIZE

[ SWAP
  []
  reducer.min
] REDUCE